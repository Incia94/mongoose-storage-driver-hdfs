description = "Mongoose HDFS storage driver"

buildscript {
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "eu.appsatori:gradle-fatjar-plugin:0.3"
	}
}

apply plugin: "java"
apply plugin: "maven"
apply plugin: "signing"
apply plugin: "eu.appsatori.fatjar"

group = "com.github.emc-mongoose"
version = "0.0.1"

tasks.withType(JavaCompile) {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

task("create-dirs").doLast(
	{
		sourceSets*.java.srcDirs*.each { it.mkdirs() }
		sourceSets*.resources.srcDirs*.each { it.mkdirs() }
	}
)

repositories {
	mavenCentral()
}

ext {

	depVersion = [
		commonsJava: "[1.0.8,)",
		hadoop: "2.8.1",
		log4j: "2.8.2",
		mongoose: "[4.0.0-alpha3,)",
	]

	setClassPath = {
		configurations -> [
			configurations.runtime
				.collect { file ->
				// it's 3rd party if jar file name doesn't contain the root project name
				if(-1 == file.name.indexOf(rootProject.name)) {
					["lib", file.name].join(File.separator)
				} else {
					file.name
				}
			}
			.join(" "),
		]
			.join(" ")
	}
}

dependencies {

	compile "com.github.emc-mongoose:mongoose-storage-driver-nio:${depVersion.mongoose}",
		{
			ext {
				fatJarExclude = true
			}
		}

	compile "com.github.emc-mongoose:mongoose-ui:${depVersion.mongoose}",
		{
			ext {
				fatJarExclude = true
			}
		}

	compile "com.github.akurilov:java-commons:${depVersion.commonsJava}",
		{
			ext {
				fatJarExclude = true
			}
		}

	compile "org.apache.logging.log4j:log4j-api:${depVersion.log4j}",
		{
			ext {
				fatJarExclude = true
			}
		}

	compile(
		"org.apache.hadoop:hadoop-common:${depVersion.hadoop}",
	)

	runtime "org.apache.logging.log4j:log4j-jul:${depVersion.log4j}",
		{
			ext {
				fatJarExclude = true
			}
		}

	runtime "org.apache.logging.log4j:log4j-slf4j-impl:${depVersion.log4j}",
		{
			ext {
				fatJarExclude = true
			}
		}

	testCompile(
		"junit:junit:[4,)",
		"org.apache.logging.log4j:log4j-api:${depVersion.log4j}",
		"org.apache.logging.log4j:log4j-core:${depVersion.log4j}",
	)
}

test {
	/*
	systemProperty "com.sun.management.jmxremote", "true"
	systemProperty "com.sun.management.jmxremote.port", "9010"
	systemProperty "com.sun.management.jmxremote.local.only", "false"
	systemProperty "com.sun.management.jmxremote.authenticate", "false"
	systemProperty "com.sun.management.jmxremote.ssl", "false"
	*/
	testLogging {
		events "passed", "skipped", "failed", "standardOut"
		showExceptions = true
		showStandardStreams = true
	}
}

jar {
	archiveName "$project.name.$extension"
	manifest {
		attributes(
			"Class-Path": setClassPath(configurations),
			"Main-Class": "com.emc.nagaina.Main",
			"Implementation-Version": version,
			"Implementation-Title": "$name"
		)
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	archiveName "$project.name-$classifier.$extension"
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = "javadoc"
	archiveName "$project.name-$classifier.$extension"
	from javadoc.destinationDir
}

artifacts {
	archives jar
	archives sourcesJar
	archives javadocJar
}

def ossrhUsername = project.hasProperty("ossrhUsername") ?
	project.property("ossrhUsername") : null
def ossrhPassword = project.hasProperty("ossrhPassword") ?
	project.property("ossrhPassword") : null

signing {
	required {
		gradle.taskGraph.hasTask("uploadArchives")
	}
	sign configurations.archives
}

// see http://central.sonatype.org/pages/gradle.html for details
uploadArchives {

	// prevent the execution for empty (not leaf) subprojects and tests packages
	if(project.name.contains("tests")) {
		return
	}

	repositories {
		mavenDeployer {
			beforeDeployment {
				MavenDeployment deployment -> signing.signPom(deployment)
			}

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom {

				groupId = rootProject.group
				name = "nagaina"
				artifactId = name

				project {
					description = rootProject.description

					url "https://github.com/emc-mongoose/nagaina/wiki"

					scm {
						connection "https://github.com/emc-mongoose/nagaina.git"
						developerConnection "https://github.com/emc-mongoose/nagaina.git"
						url "https://github.com/emc-mongoose/nagaina.git"
					}

					licenses {
						license {
							name "The MIT License (MIT)"
							url "https://github.com/emc-mongoose/nagaina/wiki/License"
						}
					}

					developers {
						developer {
							id "akurilov"
							name "Andrey Kurilov"
							email "akurilov123@gmail.com"
						}
					}
				}
			}
		}
	}
}
